apply from: '../../common.gradle'

def bintrayUser() { return localProperties().getProperty("bintrayUser") }

def bintrayKey() { return localProperties().getProperty("bintrayKey") }

if (bintrayUser() == null || bintrayKey() == null) {
    println "bintrayUser or bintrayKey not set, not applying bintrayconfig.gradle"
    return
}

apply plugin: 'com.novoda.bintray-release'

if (!project.hasProperty('publish_module_name')) {
    throw new IllegalStateException("Must specify a publish_module_name in module's build.gradle")
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publish {
    configurations = ['archives']
    bintrayUser = bintrayUser()
    userOrg = 'rene-dohan'
    groupId = 'renetik.android'
    uploadName = "renetik-android:$publish_module_name"
    artifactId = publish_module_name
    publishVersion = '1.0.1-rc1'
    desc = 'Android framework for development with pleasure'
    website = 'https://github.com/rene-dohan/renetik-android'
    dryRun = false
}